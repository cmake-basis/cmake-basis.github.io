<!DOCTYPE html PUBLIC "-//W3C//DTD XHTML 1.0 Transitional//EN"
  "http://www.w3.org/TR/xhtml1/DTD/xhtml1-transitional.dtd">


<html xmlns="http://www.w3.org/1999/xhtml">
  <head>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <meta content="Example of nested CMake superbuild of BASIS and other software dependencies." name="description" />

    <title>Nested Superbuild of BASIS and other Dependencies &mdash; BASIS</title>
    
    <link rel="stylesheet" href="../_static/cmake-basis.css" type="text/css" />
    <link rel="stylesheet" href="../_static/pygments.css" type="text/css" />
    
    <script type="text/javascript">
      var DOCUMENTATION_OPTIONS = {
        URL_ROOT:    '../',
        VERSION:     'v3.3 (dd0a896)',
        COLLAPSE_INDEX: false,
        FILE_SUFFIX: '.html',
        HAS_SOURCE:  true
      };
    </script>
    <script type="text/javascript" src="../_static/jquery.js"></script>
    <script type="text/javascript" src="../_static/underscore.js"></script>
    <script type="text/javascript" src="../_static/doctools.js"></script>
    <link rel="shortcut icon" href="../_static/favicon.ico"/>
    <link rel="author" title="About these documents" href="../about.html" />
    <link rel="top" title="BASIS" href="../index.html" />
    <link rel="up" title="Configure a Project" href="configure-project.html" />
    <link rel="next" title="Managing Test Data" href="manage-data.html" />
    <link rel="prev" title="Nested Superbuild of BASIS and other Dependencies" href="" />
   
  <meta name="viewport" content="width=device-width, initial-scale=0.9, maximum-scale=0.9">

  </head>
  <body>
  
  

    <div class="related">
      <h3>Navigation</h3>
      <ul>
        <li class="right" style="margin-right: 10px">
          <a href="manage-data.html" title="Managing Test Data"
             accesskey="N">next</a></li>
        <li class="right" >
          <a href="#" title="Nested Superbuild of BASIS and other Dependencies"
             accesskey="P">previous</a> |</li>
        <li><a href="../sidebar.html">BASIS</a> &raquo;</li>
          <li><a href="../howto.html" >How-to Guides</a> &raquo;</li>
          <li><a href="configure-project.html" accesskey="U">Configure a Project</a> &raquo;</li> 
      </ul>
    </div>  

    <div class="document">
      <div class="documentwrapper">
        <div class="bodywrapper">
          <div class="body">
            
  <div class="section" id="nested-superbuild-of-basis-and-other-dependencies">
<h1>Nested Superbuild of BASIS and other Dependencies<a class="headerlink" href="#nested-superbuild-of-basis-and-other-dependencies" title="Permalink to this headline">Â¶</a></h1>
<p>The following CMake script is an example of how to implement a nested
superbuild of BASIS and other project dependencies. It is a (edited) copy
of the <tt class="docutils literal"><span class="pre">CMakeLists.txt</span></tt> file which implements the superbuild of the
<a class="reference external" href="http://www.cbica.upenn.edu/sbia/software/dramms/download.html">DRAMMS software package</a>.</p>
<div class="highlight-cmake"><table class="highlighttable"><tr><td class="linenos"><div class="linenodiv"><pre>  1
  2
  3
  4
  5
  6
  7
  8
  9
 10
 11
 12
 13
 14
 15
 16
 17
 18
 19
 20
 21
 22
 23
 24
 25
 26
 27
 28
 29
 30
 31
 32
 33
 34
 35
 36
 37
 38
 39
 40
 41
 42
 43
 44
 45
 46
 47
 48
 49
 50
 51
 52
 53
 54
 55
 56
 57
 58
 59
 60
 61
 62
 63
 64
 65
 66
 67
 68
 69
 70
 71
 72
 73
 74
 75
 76
 77
 78
 79
 80
 81
 82
 83
 84
 85
 86
 87
 88
 89
 90
 91
 92
 93
 94
 95
 96
 97
 98
 99
100
101
102
103
104
105
106
107
108
109
110
111
112
113
114
115
116
117
118
119
120
121
122
123
124
125
126
127
128
129
130
131
132
133
134
135
136
137
138
139
140
141
142
143
144
145
146
147
148
149
150
151
152
153
154
155
156
157
158
159
160
161
162
163
164
165
166
167
168
169
170
171
172
173
174
175
176
177
178
179
180
181
182
183
184
185
186
187
188
189
190
191
192
193
194
195
196
197
198
199
200
201
202
203
204
205
206
207
208
209
210
211
212
213
214
215
216
217
218
219
220
221
222
223
224
225
226
227
228
229
230
231
232
233
234
235
236
237
238
239
240
241
242
243
244
245
246
247
248
249
250
251
252
253
254
255
256
257
258
259
260
261
262
263
264
265
266
267
268
269
270
271
272
273
274
275
276
277
278
279
280
281
282
283
284
285
286
287
288
289
290
291
292
293</pre></div></td><td class="code"><div class="highlight"><pre>##############################################################################
# @file  CMakeLists.txt
# @brief CMake configuration of bundle.
#
# Copyright (c) 2012 University of Pennsylvania. All rights reserved.
# See http://www.cbica.upenn.edu/sbia/software/license.html or COPYING file.
#
# Contact: SBIA Group &lt;sbia-software at uphs.upenn.edu&gt;
##############################################################################

cmake_minimum_required (VERSION 2.8.4)

include (ExternalProject)
include (CMakeParseArguments)

project (DRAMMSBundle)

# ============================================================================
# bundled packages
# ============================================================================

if (NOT BUNDLE_SOURCE_DIR)
  set (BUNDLE_SOURCE_DIR &quot;${CMAKE_CURRENT_SOURCE_DIR}&quot;)
endif ()

# BASIS
if (EXISTS &quot;${BUNDLE_SOURCE_DIR}/basis-2.1.4-source.tar.gz&quot;)
  set (BASIS_URL &quot;${BUNDLE_SOURCE_DIR}/basis-2.1.4-source.tar.gz&quot;)
else ()
  set (BASIS_URL &quot;INSERT DOWNLOAD URL OF PACKAGE HERE&quot;)
endif ()
set (BASIS_MD5 210be3765dde2c34f20d085ec293bed8)
# NiftiCLib
if (EXISTS &quot;${BUNDLE_SOURCE_DIR}/nifticlib-2.0.0.tar.gz&quot;)
  set (NiftiCLib_URL &quot;${BUNDLE_SOURCE_DIR}/nifticlib-2.0.0.tar.gz&quot;)
else ()
  set (NiftiCLib_URL &quot;INSERT DOWNLOAD URL OF PACKAGE HERE&quot;)
endif ()
set (NiftiCLib_MD5 ad9d7dd1ca7c10bf0592a8227c452354)
# FastPD
if (EXISTS &quot;${BUNDLE_SOURCE_DIR}/FastPD_DemoVersion.zip&quot;)
  set (FastPD_URL &quot;${BUNDLE_SOURCE_DIR}/FastPD_DemoVersion.zip&quot;)
else ()
  set (FastPD_URL &quot;INSERT DOWNLOAD URL OF PACKAGE HERE&quot;)
endif ()
set (FastPD_MD5 e8b7aa455bad254fe16434f1c601b66e)
# DRAMMS
if (NOT DRAMMS_SOURCE_DIR)
  set (DRAMMS_SOURCE_DIR &quot;${CMAKE_CURRENT_SOURCE_DIR}/..&quot;)
endif ()

# ============================================================================
# meta-data
# ============================================================================

# ----------------------------------------------------------------------------
# basis_project() macro to extract desired meta-data from BasisProject.cmake
macro (basis_project)
  CMAKE_PARSE_ARGUMENTS (ARGN &quot;&quot; &quot;NAME;VERSION&quot; &quot;&quot; ${ARGN})
  set (BUNDLE_NAME    &quot;${ARGN_NAME}&quot;)
  set (BUNDLE_VERSION &quot;${ARGN_VERSION}&quot;)
  string (TOLOWER &quot;${BUNDLE_NAME}&quot; BUNDLE_NAME_L)
  string (TOUPPER &quot;${BUNDLE_NAME}&quot; BUNDLE_NAME_U)
  unset (ARGN_VERSION)
  unset (ARGN_UNPARSED_ARGUMENTS)
endmacro ()

include (&quot;${DRAMMS_SOURCE_DIR}/BasisProject.cmake&quot;)

# ============================================================================
# global settings
# ============================================================================

if (CMAKE_INSTALL_PREFIX_INITIALIZED_TO_DEFAULT)
  if (WIN32)
    get_filename_component (CMAKE_INSTALL_PREFIX &quot;[HKEY_LOCAL_MACHINE\\SOFTWARE\\Microsoft\\Windows\\CurrentVersion;ProgramFilesDir]&quot; ABSOLUTE)
    if (NOT CMAKE_INSTALL_PREFIX OR CMAKE_INSTALL_PREFIX MATCHES &quot;/registry&quot;)
      set (CMAKE_INSTALL_PREFIX &quot;C:/Program Files&quot;)
    endif ()
    set (CMAKE_INSTALL_PREFIX &quot;${CMAKE_INSTALL_PREFIX}/SBIA/DRAMMS&quot;)
  else ()
    set (CMAKE_INSTALL_PREFIX &quot;/opt/sbia/dramms&quot;)
  endif ()
  if (BUNDLE_VERSION AND NOT BUNDLE_VERSION MATCHES &quot;^0(\\.0)?(\\.0)?$&quot;)
    set (CMAKE_INSTALL_PREFIX &quot;${CMAKE_INSTALL_PREFIX}-${BUNDLE_VERSION}&quot;)
  endif ()
  set_property (CACHE CMAKE_INSTALL_PREFIX PROPERTY VALUE &quot;${CMAKE_INSTALL_PREFIX}&quot;)
endif ()

option (BUILD_DOCUMENTATION     &quot;Whether to configure and build the documentation.&quot;  OFF)
option (TEST_BEFORE_INSTALL     &quot;Whether to run the tests before installation.&quot;      OFF)
option (USE_SYSTEM_NiftiCLib    &quot;Skip build of NiftiCLib if already installed.&quot;      OFF)
option (USE_SYSTEM_DRAMMSFastPD &quot;Skip build of patched FastPD if already installed.&quot; OFF)

if (NOT CMAKE_BUILD_TYPE)
  set_property (CACHE CMAKE_BUILD_TYPE PROPERTY VALUE &quot;Release&quot;)
endif ()

set (CMAKE_MODULE_PATH &quot;${CMAKE_CURRENT_SOURCE_DIR}&quot;)

if (NOT BUNDLE_PROJECTS)
  set (BUNDLE_PROJECTS) # tells BASIS which other packages belong to the same build
                        # each package which is build via ExternalProject_Add
                        # shall be added to this list and passed on to CMake
                        # for the configuration of any BASIS-based project
                        # using -DBUNDLE_PROJECTS:INTERNAL=&lt;names&gt;.
endif ()

# ============================================================================
# 1. BASIS
# ============================================================================

set (BUNDLE_DEPENDS) # either BASIS or nothing if BASIS already installed

# circumvent issue with CMake&#39;s find_package() interpreting these variables
# relative to the current binary directory instead of the top-level directory
if (BASIS_DIR AND NOT IS_ABSOLUTE &quot;${BASIS_DIR}&quot;)
  set (BASIS_DIR &quot;${CMAKE_BINARY_DIR}/${BASIS_DIR}&quot;)
  get_filename_component (BASIS_DIR &quot;${BASIS_DIR}&quot; ABSOLUTE)
endif ()
# moreover, users tend to specify the installation prefix instead of the
# actual directory containing the package configuration file
if (IS_DIRECTORY &quot;${BASIS_DIR}&quot;)
  list (INSERT CMAKE_PREFIX_PATH 0 &quot;${BASIS_DIR}&quot;)
endif ()

# find BASIS or build it as external project
if (DEFINED BASIS_DIR)
  find_package (BASIS REQUIRED)
else ()
  option (USE_SYSTEM_BASIS &quot;Skip build of BASIS if already installed.&quot; OFF)

  if (USE_SYSTEM_BASIS)
    find_package (BASIS QUIET)
  endif ()

  if (NOT BASIS_FOUND)
    set (BASIS_CMAKE_CACHE_ARGS)
    if (NOT BUILD_DOCUMENTATION)
      list (APPEND BASIS_CMAKE_CACHE_ARGS &quot;-DUSE_Sphinx:BOOL=OFF&quot;)
    endif ()
    if (TEST_BEFORE_INSTALL)
      find_package (ITK REQUIRED) # the test driver of BASIS yet requires ITK
      list (APPEND BASIS_CMAKE_CACHE_ARGS &quot;-DITK_DIR:PATH=${ITK_DIR}&quot;)
    else ()
      list (APPEND BASIS_CMAKE_CACHE_ARGS &quot;-DUSE_ITK:BOOL=OFF&quot;)
    endif ()
    ExternalProject_Add (
      BASIS
      PREFIX           bundle
      URL              &quot;${BASIS_URL}&quot;
      URL_MD5          ${BASIS_MD5}
      CMAKE_CACHE_ARGS &quot;-DBUNDLE_NAME:INTERNAL=${BUNDLE_NAME}&quot;
                       &quot;-DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}&quot;
                       &quot;-DBUILD_DOCUMENTATION:BOOL=OFF&quot;
                       &quot;-DBUILD_EXAMPLE:BOOL=OFF&quot;
                       &quot;-DBUILD_TESTING:BOOL=OFF&quot;
                       &quot;-DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}&quot;
                       &quot;-DBASIS_REGISTER:BOOL=OFF&quot;
                       &quot;-DBUILD_PROJECT_TOOL:BOOL=OFF&quot;
                       &quot;-DUSE_Bash:BOOL=ON&quot;
                       &quot;-DUSE_PythonInterp:BOOL=OFF&quot;
                       &quot;-DUSE_JythonInterp:BOOL=OFF&quot;
                       &quot;-DUSE_Perl:BOOL=OFF&quot;
                       &quot;-DUSE_MATLAB:BOOL=OFF&quot;
                       ${BASIS_CMAKE_CACHE_ARGS}
    )
    list (APPEND BUNDLE_DEPENDS  BASIS)
    list (APPEND BUNDLE_PROJECTS BASIS)
  endif ()
endif ()

# ============================================================================
# 2. other bundle packages
# ============================================================================

# this is by defaul done even when BASIS was found such that the build of the
# remaining packages is always the same even if it would not be necessary to
# have the external &quot;bundle&quot; project. this switch is also used by the &quot;bundle&quot;
# project to skip the addition of this external project. otherwise, it would
# be an endless recursion...
option (BUNDLE_EXTERNAL_PROJECTS &quot;Whether to bundle all external projects even if already installed BASIS is used.&quot; ON)
mark_as_advanced (BUNDLE_EXTERNAL_PROJECTS)

if (BUNDLE_EXTERNAL_PROJECTS)
  # directory of the installed BASISConfig.cmake file
  if (BUNDLE_DEPENDS MATCHES &quot;(^|;)BASIS(;|$)&quot;)
    if (BASIS_INSTALL_SCHEME MATCHES &quot;win&quot;)
      set (BASIS_DIR &quot;${CMAKE_INSTALL_PREFIX}/CMake/BASIS&quot;)
    else ()
      set (BASIS_DIR &quot;${CMAKE_INSTALL_PREFIX}/lib/cmake/${BUNDLE_NAME_L}&quot;)
    endif ()
  endif ()
  # build all other packages as external project which itself just builds
  # the following external projects. this is necessary as BASIS has to be
  # build before the other external projects can be even configured.
  # in particular the Find&lt;Pkg&gt;.cmake modules coming with BASIS are required
  # to find any already installed packages
  ExternalProject_Add (
    bundle
    DEPENDS          ${BUNDLE_DEPENDS}
    DOWNLOAD_COMMAND &quot;${CMAKE_COMMAND}&quot; -E copy &quot;${CMAKE_CURRENT_LIST_FILE}&quot; CMakeLists.txt
    PREFIX           bundle
    DOWNLOAD_DIR     bundle
    SOURCE_DIR       bundle
    BINARY_DIR       bundle
    STAMP_DIR        bundle/tmp
    TMP_DIR          bundle/tmp
    CMAKE_CACHE_ARGS &quot;-DBUNDLE_PROJECTS:STRING=${BUNDLE_PROJECTS}&quot;
                     &quot;-DBUILD_DOCUMENTATION:BOOL=${BUILD_DOCUMENTATION}&quot;
                     &quot;-DTEST_BEFORE_INSTALL:BOOL=${TEST_BEFORE_INSTALL}&quot;
                     &quot;-DUSE_SYSTEM_NiftiCLib:BOOL=${USE_SYSTEM_NiftiCLib}&quot;
                     &quot;-DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}&quot;
                     &quot;-DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}&quot;
                     &quot;-DBASIS_DIR:PATH=${BASIS_DIR}&quot;
                     &quot;-DBUNDLE_SOURCE_DIR:PATH=${BUNDLE_SOURCE_DIR}&quot;
                     &quot;-DDRAMMS_SOURCE_DIR:PATH=${DRAMMS_SOURCE_DIR}&quot;
                     &quot;-DBUNDLE_EXTERNAL_PROJECTS:INTERNAL=OFF&quot;
    INSTALL_COMMAND  &quot;&quot;
  )
  # remove all bundle files on &quot;make clean&quot;
  set_property (DIRECTORY APPEND PROPERTY ADDITIONAL_MAKE_CLEAN_FILES &quot;${CMAKE_CURRENT_BINARY_DIR}/bundle&quot;)
  # do not continue... the external &quot;bundle&quot; project will do the rest
  return ()
endif ()

set_directory_properties (PROPERTY EP_PREFIX &quot;${CMAKE_CURRENT_BINARY_DIR}&quot;)



set (DRAMMS_DEPENDS) # external projects which DRAMMS depends on
                     # note that dependencies may already be installed

# ----------------------------------------------------------------------------
# NiftiCLib
if (USE_SYSTEM_NiftiCLib)
  basis_find_package (NiftiCLib QUIET)
endif ()

if (NOT NiftiCLib_FOUND)
  ExternalProject_Add (
    NiftiCLib
    URL              &quot;${NiftiCLib_URL}&quot;
    URL_MD5          ${NiftiCLib_MD5}
    CMAKE_ARGS       -Wno-dev # suppress missing cmake_minimum_required() warning
    CMAKE_CACHE_ARGS &quot;-DBUILD_SHARED_LIBS:BOOL=OFF&quot;
                     &quot;-DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}&quot;
                     &quot;-DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_CURRENT_BINARY_DIR}&quot;
  )
  set (NiftiCLib_DIR &quot;${CMAKE_CURRENT_BINARY_DIR}&quot;)
  list (APPEND BUNDLE_PROJECTS &quot;NiftiCLib&quot;)
  list (APPEND DRAMMS_DEPENDS  &quot;NiftiCLib&quot;)
endif ()

# ----------------------------------------------------------------------------
# FastPD
if (USE_SYSTEM_DRAMMSFastPD)
  basis_find_package (DRAMMSFastPD QUIET)
endif ()

if (NOT DRAMMSFastPD_FOUND)
  ExternalProject_Add (
    FastPD
    URL              &quot;${FastPD_URL}&quot;
    URL_MD5          ${FastPD_MD5}
    PATCH_COMMAND    patch -p1 &lt; &quot;${BUNDLE_SOURCE_DIR}/FastPD.patch&quot;
    CMAKE_CACHE_ARGS &quot;-DBUILD_SHARED_LIBS:BOOL=OFF&quot;
                     &quot;-DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}&quot;
                     &quot;-DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_CURRENT_BINARY_DIR}&quot;
  )
  set (DRAMMSFastPD_DIR &quot;${CMAKE_CURRENT_BINARY_DIR}/lib&quot;)
  list (APPEND BUNDLE_PROJECTS &quot;FastPD&quot;)
  list (APPEND DRAMMS_DEPENDS  &quot;FastPD&quot;)
endif ()

# ----------------------------------------------------------------------------
# DRAMMS
ExternalProject_Add (
  DRAMMS
  DEPENDS             ${DRAMMS_DEPENDS}
  SOURCE_DIR          &quot;${DRAMMS_SOURCE_DIR}&quot;
  CMAKE_CACHE_ARGS    &quot;-DBASIS_DIR:PATH=${BASIS_DIR}&quot;
                      &quot;-DNiftiCLib_DIR:PATH=${NiftiCLib_DIR}&quot;
                      &quot;-DDRAMMSFastPD_DIR:PATH=${DRAMMSFastPD_DIR}&quot;
                      &quot;-DBUNDLE_NAME:INTERNAL=${BUNDLE_NAME}&quot;
                      &quot;-DBUNDLE_PROJECTS:INTERNAL=${BUNDLE_PROJECTS}&quot;
                      &quot;-DBASIS_ALL_DOC:BOOL=${BUILD_DOCUMENTATION}&quot;
                      &quot;-DCMAKE_BUILD_TYPE:STRING=${CMAKE_BUILD_TYPE}&quot;
                      &quot;-DBUILD_DOCUMENTATION:BOOL=${BUILD_DOCUMENTATION}&quot;
                      &quot;-DBUILD_TESTING:BOOL=${TEST_BEFORE_INSTALL}&quot;
                      &quot;-DCMAKE_INSTALL_PREFIX:PATH=${CMAKE_INSTALL_PREFIX}&quot;
  TEST_BEFORE_INSTALL ${TEST_BEFORE_INSTALL}
)
</pre></div>
</td></tr></table></div>
</div>


          </div>
        </div>
      </div>
      <div class="sphinxsidebar">
        <div class="sphinxsidebarwrapper">
  <p class="logo"><a href="../index.html">
    <img class="logo" src="../_static/logo_title.svg" alt="Logo"/>
  </a></p>
<div id="searchbox" style="display: none">
  <h3>Quick search</h3>
    <form class="search" action="../search.html" method="get">
      <input type="text" name="q" />
      <input type="submit" value="Go" />
      <input type="hidden" name="check_keywords" value="yes" />
      <input type="hidden" name="area" value="default" />
    </form>
    <p class="searchtip" style="font-size: 90%">
    Enter search terms.
    </p>
</div>
<script type="text/javascript">$('#searchbox').show(0);</script>
<h3><a href="../contents.html">Table Of Contents</a></h3>
<ul>
<li class="toctree-l1"><a class="reference internal" href="../index.html">Overview</a></li>
<li class="toctree-l1"><a class="reference internal" href="../quickstart.html">Quick Start</a></li>
<li class="toctree-l1"><a class="reference internal" href="../howto.html">How-to Guides</a></li>
<li class="toctree-l1"><a class="reference internal" href="../standard.html">Standards</a></li>
<li class="toctree-l1"><a class="reference internal" href="../guideline.html">Guidelines</a></li>
<li class="toctree-l1"><a class="reference internal" href="../reference.html">Reference</a></li>
<li class="toctree-l1"><a class="reference internal" href="../apidoc.html">API</a></li>
</ul>
<ul>
<li class="toctree-l1"><a class="reference internal" href="../changelog.html">News</a></li>
<li class="toctree-l1"><a class="reference internal" href="../download.html">Download</a></li>
<li class="toctree-l1"><a class="reference internal" href="../install.html">Installation</a></li>
<li class="toctree-l1"><a class="reference internal" href="../help.html">Support</a></li>
<li class="toctree-l1"><a class="reference internal" href="../about.html">About</a></li>
</ul>

<h3>External Links</h3>
<ul>
  <li><a href="https://github.com/cmake-basis/BASIS">Repository</a></ li>
  <li><a href="https://github.com/cmake-basis/BASIS/issues">Issues</a></ li>
</ul>
        </div>
      </div>
      <div class="clearer"></div>
    </div>
  <div class="footer">
    &copy; Copyright 2011-12 University of Pennsylvania, 2013-14 Carnegie Mellon University, 2013-16 Andreas Schuh.
    Created using <a href="http://sphinx-doc.org/">Sphinx</a> 1.2.2.
  </div>
  
  </body>
</html>